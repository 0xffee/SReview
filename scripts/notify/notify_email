#!/usr/bin/perl

use strict;
use warnings;

use Email::Sender::Simple qw(sendmail);
use Email::Simple;
use DBI;
use Data::Dumper;

our $config;

require './config.pl';

my $dbh = DBI->connect($config->{dbistring}, '', '') or die "Cannot connect to database!";

my $talkid = $ARGV[0];

my $recps = $dbh->prepare("SELECT speakers.name, speakers.email FROM speakers JOIN speakers_talks ON speakers_talks.speaker = speakers.id WHERE speakers_talks.talk = ? AND speakers.email IS NOT NULL");
my $ccs = $dbh->prepare("SELECT tracks.name, tracks.email FROM tracks JOIN talks ON (talks.room = tracks.room AND talks.endtime <= tracks.endtime AND talks.starttime >= tracks.starttime) WHERE talks.id = ?");
my $talk = $dbh->prepare("SELECT title, nonce FROM talks WHERE id = ?");

$recps->execute($talkid);
my $recplist = $recps->fetchall_arrayref();
my @speakers;

$ccs->execute($talkid);
my $trackmgrlist = $ccs->fetchall_arrayref();
my @trackmgrs;

foreach my $speaker(@$recplist) {
	if(defined($speaker->[1])) {
		push @speakers, $speaker->[0] . "<" . $speaker->[1] . ">";
	}
}

foreach my $trackmgr(@$trackmgrlist) {
	if(defined($trackmgr->[1])) {
		push @trackmgrs, "managers for devroom " . $trackmgr->[0] . "<" . $trackmgr->[1] . ">";
	}
}

if(scalar(@trackmgrs) == 0 && scalar(@speakers) == 0) {
	die "no addressees, can't send email!";
}

$talk->execute($talkid);
my $talkdata = $talk->fetchrow_hashref;
my $title = $talkdata->{title};
my $save = $/;
$/ = '/';
chomp($config->{urlbase});
$/ = $save;
my $url = join('/', ($config->{urlbase}, "review", $talkdata->{nonce}));

my $body = <<EOF;
Hi,

This is to inform you that a preview of the recording of the talk titled

$title

is now available at

$url

for you to review.

Please note that with the packed schedule at FOSDEM, and the requirement
of manual review for every video, it takes a lot of work for one person,
or even just one team, to handle the review workload. Yet, reviewing
just one video is often no more than a few minutes of work. If you were
to help us out, we could release your video much sooner than would
otherwise be the case.

If you do not find the time to help us out with this, we will eventually
get around to it ourselves, but this will take much longer.

In particular, you should pay attention to:
- Does the video start at the correct time? If not, enter new start and
  length values, or use the buttons below the video elements to help you
  calculate the correct values.
- Is the audio usable? If not, you may want to try to use the alternate
  audio channel.

If you have any further questions, note that this email was sent from an
address that does not receive email. If you want to reach us, you can do
so via:

IRC: irc.freenode.net, #fosdem-video
email: video\@lists.fosdem.org
or just fill out the "comments" field in the URL above

Thank you for your cooperation,

The FOSDEM video team
EOF

my $email = Email::Simple->create(header =>
	       	[
	      	 	From => '<noreply@video.fosdem.org>',
	       		To => join(',',@speakers),
	       		Subject => "Video of your FOSDEM talk entitled \"$title\" ready for review",
	       		Cc => join(',',@trackmgrs)
	       	],
	       	body => $body
	);

sendmail($email);
