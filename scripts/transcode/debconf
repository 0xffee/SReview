#!/usr/bin/perl -w

use strict;
use warnings;

use DBI;
use Glib qw/G_PRIORITY_DEFAULT/;
use Glib::Object::Introspection;
use File::Path qw/make_path/;
use File::Temp qw/tempdir/;

Glib::Object::Introspection->setup(
		basename => "Gst",
		version => "1.0",
		package => "Gst",
	);

my $mainloop = Glib::MainLoop->new();

Gst::init(\@ARGV);

our $config;
require "./config.pl";

my $dbh = DBI->connect($config->{dbistring}, '', '') or die "Cannot connect to database!";

$dbh->begin_work;

my $talkid = $ARGV[0];

my $talk = $dbh->prepare("SELECT * FROM talks WHERE id = ?");

$talk->execute($talkid);

my $row = $talk->fetchrow_hashref();
my $slug = $row->{slug};

sub buswatch {
	my $bus = shift;
	my $msg = shift;
	my $data = shift;

	if($msg->type >= "eos") {
		$mainloop->quit();
	}

	return 1;
}

my $data = $dbh->prepare("SELECT events.id AS eventid, events.name AS event, rooms.name AS room, talks.starttime::date, talks.slug FROM talks JOIN events ON talks.event = events.id JOIN rooms ON rooms.id = talks.room WHERE talks.id = ?");
$data->execute($talkid);
my $drow = $data->fetchrow_hashref();
my $pubdir = $config->{pubdir};
my $eid = $drow->{eventid};
make_path("$pubdir/video/$eid");
my $outputsub = $config->{outputdir};
my $outputdir = &$outputsub($drow->{event}, $drow->{room}, $drow->{starttime});
my $tmpdir = tempdir( "transXXXXXX", DIR => $config->{workdir}, CLEANUP => 1);
my @pipelines = (
	"webmmux name=mux ! fakesink uridecodebin uri=file://$outputdir/$slug.mp4 name=demux demux. ! deinterlace ! videoconvert ! videoscale ! vp8enc threads=1 multipass-cache-file=$tmpdir/vp8-multipass multipass-mode=1 ! queue ! mux.video_0 demux. ! progressreport ! audioconvert ! audiorate ! vorbisenc ! queue ! mux.audio_0",
	"webmmux name=mux ! filesink location=$outputdir/$slug.webm uridecodebin uri=file://$outputdir/$slug.mp4 name=demux demux. ! deinterlace ! videoconvert ! videoscale ! vp8enc threads=1 multipass-cache-file=$tmpdir/vp8-multipass multipass-mode=2 ! queue ! mux.video_0 demux. ! progressreport ! audioconvert ! audiorate ! vorbisenc ! queue ! mux.audio_0",
);

foreach my $pipeline_str(@pipelines) {
	my $pipeline = Gst::parse_launch($pipeline_str);
	my $bus = $pipeline->get_bus();
	$bus->add_watch(G_PRIORITY_DEFAULT, \&buswatch, $mainloop);
	$pipeline->set_state("playing");
	$mainloop->run();
}

my $update = $dbh->prepare("UPDATE talks SET state = state_next(state) WHERE id = ? AND state = 'generating_data'");
$update->execute($talkid);
$dbh->commit;
