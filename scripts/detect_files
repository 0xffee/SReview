#!/usr/bin/perl -w

use strict;
use warnings;

use DBI;

use Glib;
use Glib::Object::Introspection;

Glib::Object::Introspection->setup(
	basename => "Gst",
	version => "1.0",
	package => "Gst",
	);
Glib::Object::Introspection->setup(
	basename => "GstPbutils",
	version => "1.0",
	package => "Gst::Pbutils",
	);

our $config;
require './config.pl';

my $dbh = DBI->connect($config->{dbistring}, '', '') or die "Cannot connect to database!";

$dbh->begin_work;

my $exists = $dbh->prepare("SELECT count(*) FROM raw_files WHERE filename = ?");
my $add = $dbh->prepare("INSERT INTO raw_files(filename, room, starttime, endtime) VALUES (?, (SELECT id FROM rooms WHERE name = ?), ?::timestamptz, ?::timestamptz + ?::interval)");

my @files = glob($config->{inputdir} . "/*/*/*" . $config->{inputext});

Gst::init(\@ARGV);
my $disc = Gst::Pbutils::Discoverer->new(1_000_000_000);

foreach my $file(@files) {
	next unless (-f $file);
	$exists->execute($file);
	my $row = $exists->fetchrow_hashref;
	if($row->{count} eq '0') {
		my $ext = $config->{inputext};
		next unless $file =~ /.*\/([^\/]*)\/([^\/]*)\/(.*)${ext}$/;
		my $room = $1;
		my $start_day = $2;
		my $start_hour = $3;
		$start_hour =~ s/_/:/g;
		my $info = $disc->discover_uri(Glib->filename_to_uri($file, undef));
		my $length = $info->get_duration() / 1_000_000_000;
		$add->execute($file, $room, "$start_day $start_hour", "$start_day $start_hour", "$length seconds");
	}
}

my $full = $dbh->prepare("UPDATE talks SET state = 'files_found' WHERE state < 'files_found' AND id IN (select talkid FROM raw_talks WHERE talks_interval <= raw_total)");
$full->execute();
my $partial = $dbh->prepare("UPDATE talks SET state = 'partial_files_found' WHERE state < 'partial_files_found' AND id IN (select distinct talkid FROM raw_talks)");
$partial->execute();

$dbh->commit;
