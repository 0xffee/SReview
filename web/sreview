#!/usr/bin/perl -w

# Sreview, a web-based video review and transcoding system
# Copyright (c) 2016-2017, Wouter Verhelst <w@uter.be>
#
# Sreview is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published
# by the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public
# License along with this program.  If not, see
# <http://www.gnu.org/licenses/>.

use strict;
use warnings;

use Mojolicious::Lite;
use Mojo::Collection 'c';
use Mojo::JSON qw(encode_json);
use DBI;

our $config;
require './config.pl';

die if $config->{secrets}[0] eq "_INSECURE_DEFAULT_REPLACE_ME_";

my $dbh = DBI->connect($config->{dbistring}, '', '', {AutoCommit => 1}) or die "Cannot connect to database!";

sub find_event {
	my $st = $dbh->prepare("SELECT id FROM events WHERE name = ?");
	$st->execute($config->{event}) or die "Could not find event!\n";
	while(my $row = $st->fetchrow_hashref("NAME_lc")) {
		die if exists($config->{eventid});
		$config->{eventid} = $row->{id};
	}
}

find_event;

app->secrets($config->{secrets});

helper dbh => sub { return $dbh; };

my $version;

sub get_version {
	open GIT, "git describe --tags --dirty|";
	$version = <GIT>;
	chomp $version;
	close GIT;
}

get_version;

helper talk_update => sub {
	my $c = shift;
	my $talk = shift;
	my $choice = $c->param('choice');
	if(!defined($choice)) {
		die "choice empty";
	} elsif($choice eq 'reset') {
		my $sth = $c->dbh->prepare("UPDATE talks SET state='preview', progress='waiting' WHERE id = ?");
		$sth->execute($talk) or die;
	} elsif($choice eq 'ok') {
		my $sth = $c->dbh->prepare("UPDATE talks SET state='preview', progress='done' WHERE id = ?");
		$sth->execute($talk) or die;
	} elsif($choice eq 'standard') {
		my $sth = $c->dbh->prepare("SELECT id, name FROM properties");
		$sth->execute();
		while(my $row = $sth->fetchrow_hashref("NAME_lc")) {
			my $name = $row->{name};
			my $parm = $c->param("correction_${name}");
			next unless defined($parm);
			next if (length($parm) == 0);
			my $s = $c->dbh->prepare("INSERT INTO corrections(property_value, talk, property) VALUES(?, ?, ?)");
			$s->execute($parm, $talk, $row->{id}) or die;
		}
		$sth = $c->dbh->prepare("UPDATE talks SET state='waiting_for_files', progress='done' WHERE id = ?");
		$sth->execute($talk) or die;
	} elsif($choice eq 'comments') {
		my $sth = $c->dbh->prepare("UPDATE talks SET state='broken', progress='failed', comments = ? WHERE id = ?");
		my $comments = $c->param("comment_text");
		$sth->execute($comments, $talk) or die;
	} else {
		$c->stash(message => "Unknown action.");
		$c->render("error");
		return undef;
	}
	$c->stash(message => 'Update successful.');
};


helper version => sub { return $version; };

get '/' => sub {
	my $c = shift;
	$c->render;
} => 'index';

get '/login';

post '/login_post' => sub {
	my $c = shift;

	my $email = $c->param('email');
	my $pass = $c->param('pass');

	my $st = $c->dbh->prepare("SELECT id, isadmin, isvolunteer, name, room FROM users WHERE email=? AND password=crypt(?, password)");
	my $rv;
	if(!($rv = $st->execute($email, $pass))) {
		die "Could not check password: " . $st->errstr;
	}
	if($rv == 0) {
		$c->stash(message => "Incorrect username or password.");
		$c->render('error');
		return undef;
	}
	my $row = $st->fetchrow_arrayref or die "eep?! username query returned nothing\n";
	$c->session->{id} = $row->[0];
	$c->session->{email} = $email;
	$c->session->{admin} = $row->[1];
	$c->session->{volunteer} = $row->[2];
	$c->session->{name} = $row->[3];
	$c->session->{room} = $row->[4];

	if($c->session->{volunteer}) {
		return $c->redirect_to('/volunteer/list');
	} else {
		return $c->redirect_to('/admin');
	}
};

get '/review/:nonce' => sub {
	my $c = shift;
	my $stt = $c->dbh->prepare("SELECT state, name, id, extract(epoch from prelen) as prelen, extract(epoch from postlen) as postlen, extract(epoch from (endtime - starttime)) as length, speakers, starttime, endtime, slug, room, comments FROM talk_list WHERE nonce=?");
	my $rv = $stt->execute($c->param("nonce"));
	if($rv == 0) {
		$c->res->code(404);
		$c->render(text => "Invalid URL");
		return undef;
	}
	my $row = $stt->fetchrow_hashref("NAME_lc");
	if($row->{state} ne 'preview' && $row->{state} ne 'broken') {
		$c->stash(message => "The talk <q>" . $row->{name} . "</q> is not currently available for review. It is in the state <tt>" . $row->{state} . "</tt>, whereas we need the <tt>preview</tt> state to do review. For more information, please see <a href='https://yoe.github.io/sreview/'>the documentation</a>");
		$c->stash(title => 'Review finished or not yet available.');
		$c->render('msg');
		return undef;
	}
	my $stp = $c->dbh->prepare("SELECT properties.name, properties.description, properties.helptext, corrections.property_value FROM properties left join corrections on (properties.id = corrections.property AND talk = ?) ORDER BY properties.description");
	$stp->execute($row->{id});
	my $viddata = {};
	$viddata->{corrvals} = {};
	$viddata->{corrdescs} = {};
        $viddata->{corrhelps} = {};
	while(my $corrrow = $stp->fetchrow_hashref) {
		$viddata->{corrdescs}{$corrrow->{name}} = $corrrow->{description};
		$viddata->{corrhelps}{$corrrow->{name}} = $corrrow->{helptext};
		$viddata->{corrvals}{$corrrow->{name}} = $corrrow->{property_value} + 0;
	}

	$viddata->{mainlen} = $row->{length} + 0;
	$viddata->{prelen} = $row->{prelen} + 0;
	$viddata->{postlen} = $row->{postlen} + 0;

	$c->stash(title => 'Review for ' . $row->{name});
	$c->stash(talk_title => $row->{name});
	$c->stash(talk_speakers => $row->{speakers});
	$c->stash(talk_start => $row->{starttime});
	$c->stash(talk_end => $row->{endtime});
	$c->stash(slug => $row->{slug});
	$c->stash(event => $config->{event});
	$c->stash(eventid => $config->{eventid});
	$c->stash(room => $row->{room});
	$c->stash(state => $row->{state});
	$c->stash(corrections => $viddata);
	$c->stash(comments => $row->{comments});
	$c->stash(target => "talk_update");
	$c->stash(layout => 'default');
	$c->stash(script_raw => 'sreview_viddata = ' . encode_json($viddata) . ';');
} => 'talk';

get '/overview' => sub {
	my $c = shift;
	my $st = $c->dbh->prepare('SELECT name, speakers, room, starttime, state, progress FROM talk_list WHERE eventid = ? AND state IS NOT NULL ORDER BY state, room, starttime, progress');
	my $tot = $c->dbh->prepare('SELECT state, count(*) FROM talks WHERE event = ? GROUP BY state ORDER BY state;');
	my %expls;
	my $tot_results;
	my $totals = [];
	$expls{'waiting_for_files'} = 'Still waiting for content files for these talks';
	$expls{'cutting'} = 'Talk is being cut';
	$expls{'generating_previews'} = 'Talk previews are being generated';
	$expls{'notification'} = 'Sending out notifications';
	$expls{'preview'} = 'Talk ready for review, waiting for reviewer';
	$expls{'transcoding'} = 'High-quality transcodes running';
	$expls{'uploading'} = 'Publishing results';
	$expls{'done'} = 'Videos published, all done';
	$expls{'broken'} = 'Review found problems, administrator required';
	$expls{'needs_work'} = 'Fixable problems exist, manual intervention required';
	$expls{'lost'} = 'Nonfixable problems exist, talk lost';
	$st->execute($config->{eventid}) or die;
	$tot->execute($config->{eventid}) or die;
	$c->stash(title => 'Video status overview');
	$c->stash(titlerow => [ 'Talk', 'Speakers', 'Room', 'Start time', 'State', 'Progress' ]);
	$c->stash(tottitrow => [ 'State', 'Count', 'State meaning']);
	$c->stash(rows => $st->fetchall_arrayref);
	$c->stash(header => 'Video status overview');
	$tot_results = $tot->fetchall_arrayref();
	foreach my $row(@{$tot_results}) {
		push @$row, $expls{$row->[0]};
		push @$totals, $row;
	}
	$c->stash(totals => $totals);
	$c->stash(layout => 'default');
	$c->render;
} => 'table';

post '/talk_update' => sub {
	my $c = shift;
	my $nonce = $c->param("nonce");
	if(!defined($nonce)) {
		$c->stash(message=>"Unauthorized.");
		$c->res->code(403);
		$c->render('error');
		return undef;
	}
	my $sth = $c->dbh->prepare("SELECT id FROM talks WHERE nonce = ? AND state IN ('preview', 'broken')");
	$sth->execute($nonce);
	my $row = $sth->fetchrow_arrayref;
	if(scalar($row) == 0) {
		$c->stash(message=>"Change not allowed. If this talk exists, it was probably reviewed by someone else while you were doing so too. Please try again later, or check the overview page.");
		$c->res->code(403);
		$c->render('error');
		return undef;
	}
	$c->stash(layout => 'default');
	$c->stash(template => 'talk');
	$c->flash(completion_message => 'Your change has been accepted. Thanks for your help!');
	$c->talk_update($row->[0]);
	$c->redirect_to("/review/$nonce");
} => 'talk_update';

### VOLUNTEER REVIEW ###

group {
	under '/volunteer' => sub {
		my $c = shift;
		if(!exists($c->session->{id})) {
			$c->redirect_to('/login');
			return 0;
		}
		$c->stash(id => $c->session->{id});
		return 1;
	};

	get '/list' => sub {
		my $c = shift;
		my @talks;
		$c->dbh->begin_work;
		my $already = $c->dbh->prepare("SELECT nonce, title, id, state FROM talks WHERE reviewer = ? AND state < 'review_done'");
		my $new = $c->dbh->prepare("SELECT nonce, title, id, state FROM talks WHERE reviewer IS NULL AND state = ANY(enum_range('files_found'::talkstate, 'preview'::talkstate)) LIMIT ? FOR UPDATE");
		my $claim = $c->dbh->prepare("UPDATE talks SET reviewer = ? WHERE id = ?");
		$already->execute($c->session->{id});
		my $count = $already->rows;
		if($count < 3) {
			$new->execute(3 - $count);
		}
		for(my $i = 0; $i < $count; $i++) {
			my $row = [ $already->fetchrow_array ];
			push @talks, $row;
		}
		for(my $i = 0; $i < $new->rows; $i++) {
			my $row = [ $new->fetchrow_array ];
			$claim->execute($c->session->{id}, $row->[2]);
			push @talks, $row;
		}
		$c->stash(talks => \@talks);
		$c->stash(layout => 'admin');
		$c->dbh->commit;
	} => 'volunteer/list';
};

### ADMINISTRATION ###

group {
	under '/admin' => sub {
		my $c = shift;
		if(!exists($c->session->{id})) {
			$c->res->code(403);
			$c->redirect_to('/login');
			return 0;
		}
		if($c->session->{volunteer}) {
			$c->redirect_to('/volunteer/list');
			return 0;
		}
		$c->stash(layout => "admin");
		$c->stash(admin => $c->session->{admin});
		return 1;
	};

	get '/' => sub {
		my $c = shift;
		my $st;
		my $talks = ();
		my $room;
		my $lastroom = '';

		if(defined($c->session->{room})) {
			$st = $c->dbh->prepare('SELECT id, room, name, starttime, speakers, state FROM talk_list WHERE eventid = ? AND roomid = ? ORDER BY starttime');
			$st->execute($config->{eventid}, $c->session->{room});
		} else {
			$st = $c->dbh->prepare('SELECT id, room, name, starttime, speakers, state FROM talk_list WHERE eventid = ? ORDER BY room, starttime');
			$st->execute($config->{eventid});
		}
		while(my $row = $st->fetchrow_hashref("NAME_lc")) {
			if ($row->{'room'} ne $lastroom) {
				if(defined($room)) {
					push @$talks, c($lastroom => $room);
				}
				$room = [];
			}
			$lastroom = $row->{'room'};
			next unless defined($row->{id});
			push @$room, [$row->{'starttime'} . ': ' . $row->{'name'} . ' by ' . $row->{'speakers'} . ' (' . $row->{'state'} . ')' => $row->{'id'}];
		}
		if(defined($room)) {
			push @$talks, c($lastroom => $room);
		}
		$c->stash(email => $c->session->{email});
		$c->stash(talks => $talks);
		$c->render;
	} => 'admin/main';

	get '/logout' => sub {
		my $c = shift;
		delete $c->session->{id};
		delete $c->session->{room};
		$c->redirect_to('/');
	};

	get '/talk' => sub {
		my $c = shift;
		my $id = $c->param("talk");
		my $st;

		if(defined($c->session->{room})) {
			$st = $c->dbh->prepare('SELECT state, name, id, extract(epoch from prelen) as prelen, extract(epoch from postlen) as postlen, extract(epoch from (endtime - starttime)) as length, speakers, starttime, endtime, slug, room, comments, apologynote FROM talk_list WHERE id = ? AND roomid = ?');
			$st->execute($id, $c->session->{room});
		} else {
			$st = $c->dbh->prepare('SELECT state, name, id, extract(epoch from prelen) as prelen, extract(epoch from postlen) as postlen, extract(epoch from (endtime - starttime)) as length, speakers, starttime, endtime, slug, room, comments, apologynote FROM talk_list WHERE id = ?');
			$st->execute($id);
		}
		my $row = $st->fetchrow_hashref("NAME_lc");
		my $stp = $c->dbh->prepare("SELECT properties.name, properties.description, properties.helptext, corrections.property_value FROM properties left join corrections on (properties.id = corrections.property AND corrections.talk = ?) ORDER BY properties.description");
		$stp->execute($id);

		if(!defined($row)) {
			$c->stash(message => "Unknown talk.");
			$c->render('error');
			return undef;
		}
		my $viddata = {};
		$viddata->{corrvals} = {};
		$viddata->{corrdescs} = {};
		$viddata->{corrhelps} = {};
		while(my $corrrow = $stp->fetchrow_hashref) {
			$viddata->{corrdescs}{$corrrow->{name}} = $corrrow->{description};
			$viddata->{corrvals}{$corrrow->{name}} = $corrrow->{property_value} + 0;
			$viddata->{corrhelps}{$corrrow->{name}} = $corrrow->{helptext};
		}
		$viddata->{mainlen} = $row->{length} + 0;
		$viddata->{prelen} = $row->{prelen} + 0;
		$viddata->{postlen} = $row->{postlen} + 0;

		$c->stash(talk_title => $row->{name});
		$c->stash(talk_speakers => $row->{speakers});
		$c->stash(talk_start => $row->{starttime});
		$c->stash(talk_end => $row->{endtime});
		$c->stash(slug => $row->{slug});
		$c->stash(event => $config->{event});
		$c->stash(eventid => $config->{eventid});
		$c->stash(room => $row->{room});
		$c->stash(state => $row->{state});
		$c->stash(comments => $row->{comments});
		$c->stash(corrections => $viddata);
		$c->stash(target => "talk_update_admin");
		$c->stash(script_raw => 'sreview_viddata = ' . encode_json($viddata) . ';');
		$c->stash(type => "admin");
		$c->stash(apology => $row->{apologynote});
		$c->render(template => 'talk');
	} => 'admin_talk';

	post '/talk_update' => sub {
		my $c = shift;
		my $talk = $c->param("talk");
		if(!defined($talk)) {
			$c->stash(message => "Required parameter talk missing.");
			$c->render("error");
			return undef;
		}
		$c->stash(template => 'talk');
		$c->flash(completion_message => 'Your change has been accepted. Thanks for your help!');
		$c->talk_update($talk);
		$c->redirect_to("/admin/talk?talk=$talk");
	} => 'talk_update_admin';

	get '/brokens' => sub {
		my $c = shift;
		my $st = $c->dbh->prepare("SELECT talks.id, title, speakeremail(talks.id), tracks.email, comments, state FROM talks JOIN tracks ON talks.track = tracks.id WHERE state>='broken' ORDER BY state,id");
		my $tst = $c->dbh->prepare("SELECT rooms.altname, count(talks.id) FROM talks JOIN rooms ON talks.room = rooms.id WHERE talks.state='broken' GROUP BY rooms.altname");
		my $rows = [];
		$st->execute;
		$tst->execute;
		$c->stash(title => 'Broken talks');
		$c->stash(titlerow => [ 'id', 'Title', 'Speakers', 'Track email', 'Comments', 'State', 'Link' ]);
		$c->stash(tottitrow => [ 'Room', 'Count' ]);
		my $pgrows = $st->fetchall_arrayref;
		foreach my $row(@{$pgrows}) {
			push @$row, "<a href='/admin/talk?talk=" . $row->[0] . "'>review</a>";
			push @$rows, $row;
		}
		$c->stash(rows => $rows);
		$c->stash(totals => $tst->fetchall_arrayref);
		$c->stash(header => 'Broken talks');
		$c->stash(layout => 'admin');
		$c->stash(totals => undef);
		$c->render(template => 'table');
	} => 'broken_table';

	under '/admin/system' => sub {
		my $c = shift;
		if(!exists($c->session->{id})) {
			$c->res->code(403);
			$c->render(text => 'Unauthorized (not logged on)');
			return 0;
		}
		if(!$c->session->{admin}) {
			$c->res->code(403);
			$c->render(text => 'Unauthorized (not admin)');
			return 0;
		}
		$c->stash(layout => "admin");
		$c->stash(admin => $c->session->{admin});
		return 1;
	};

	get '/' => sub {
		my $c = shift;
		$c->stash(email => $c->session->{email});
		my $st = $dbh->prepare("SELECT DISTINCT rooms.id, rooms.name FROM rooms LEFT JOIN talks ON rooms.id = talks.room WHERE talks.event = ?");
		$st->execute($config->{eventid});
		my $rooms = [['All rooms' => '', selected => 'selected']];
		while(my $row = $st->fetchrow_arrayref) {
			push @$rooms, [$row->[1] => $row->[0]];
		}
		$c->stash(rooms => $rooms);
	} => 'admin/dashboard';

	get '/adduser' => sub {
		my $c = shift;
		open PASSWORD, "pwgen -s 10 -n 1|";
		my $password = <PASSWORD>;
		close(PASSWORD);
		chomp $password;
		my $st = $c->dbh->prepare("INSERT INTO users(email, name, isadmin, isvolunteer, password, room) VALUES(?, ?, ?, ?, crypt(?, gen_salt('bf', 8)), ?)");
		my $room = $c->param('rooms');
		if($room eq "") {
			$room = undef;
		}
		$st->execute($c->param('email'), $c->param('name'), $c->param('isadmin'), $c->param('isvolunteer'), $password, $room) or die;
		$c->dbh->prepare("UPDATE users SET isadmin = false WHERE isadmin is null")->execute;
		$c->flash(msg => "User with email " . $c->param('email') . " created, with password '$password'");
		$c->redirect_to('/admin/system');
	};

	get '/chpw' => sub {
		my $c = shift;
		my $st = $c->dbh->prepare("SELECT * FROM users WHERE email=?");
		my $email = $c->param("email");
		$st->execute($email);
		if($st->rows != 1) {
			$c->flash(msg => "There is no user with email address " . $c->param('email') . ". Try creating it?");
			$c->redirect_to('/admin/system');
			return;
		}
		open PASSWORD, "pwgen -s 10 -n 1|";
		my $password = <PASSWORD>;
		close(PASSWORD);
		chomp $password;
		$st = $c->dbh->prepare("UPDATE users SET password = crypt(?, gen_salt('bf', 8)) WHERE email = ?");
		$st->execute($password, $email);
		$c->flash(msg => "Password for user $email set to '$password'");
		$c->redirect_to('/admin/system');
	};

	get '/setpw' => sub {
		my $c = shift;
		my $pw1 = $c->param('password1');
		my $pw2 = $c->param('password2');
		if ($pw1 ne $pw2) {
			$c->flash(msg => "Passwords did not match!");
			$c->redirect_to('/admin/system');
			return;
		}
		my $st = $c->dbh->prepare("UPDATE users SET password = crypt(?, gen_salt('bf', 8)) WHERE email = ?");
		$st->execute($pw1, $c->session->{email});
		$c->flash(msg => "Password changed.");
		$c->redirect_to('/admin/system');
	};
};

get '*any' => sub {
	my $c = shift;
	$c->redirect_to('/overview');
};

app->start;
